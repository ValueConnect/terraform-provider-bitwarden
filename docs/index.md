---
layout: ""
page_title: "Bitwarden Provider"
description: |-
  Use the Bitwarden provider to read, create, or update logins, secure notes and folders in your Bitwarden Vaults.
---

# Bitwarden Provider

Use the Bitwarden provider to interact with Bitwarden logins, secure notes and folders.
You must configure the provider with the proper credentials before you can use it.

## Example Usage

```terraform
# Bitwarden Credentials
variable "bw_password" {
  type        = string
  description = "Bitwarden Master Key"
  sensitive   = true
}

variable "bw_client_id" {
  type        = string
  description = "Bitwarden Client ID"
  sensitive   = true
}

variable "bw_client_secret" {
  type        = string
  description = "Bitwarden Client Secret"
  sensitive   = true
}
```

```terraform
# Provider configuration
terraform {
  required_providers {
    bitwarden = {
      source  = "maxlaverse/bitwarden"
      version = "0.0.1"
    }
  }
  required_version = ">= 1.0.2"
}

provider "bitwarden" {
  master_password = var.bw_password
  client_id       = var.bw_client_id
  client_secret   = var.bw_client_secret
  email           = "test@laverse.net"
  server          = "https://vault.bitwarden.com"
}
```

```terraform
# Save sensitive Terraform generated data to Bitwarden
resource "bitwarden_folder" "terraform-bw-folder" {
  name = "Terraform Generated"
}

resource "bitwarden_item_login" "vpn-read-only-userpwd" {
  name      = "VPN Read Only User/Password Access"
  username  = "vpn-read-only"
  password  = some_other_plugin.user-read-only.secret
  folder_id = bitwarden_folder.terraform-bw-folder.id
}

resource "bitwarden_item_secure_note" "vpn-read-only-certs" {
  name      = "VPN Read Only Certificate Access"
  notes     = some_other_plugin.user-read-only.private_key
  folder_id = bitwarden_folder.terraform-bw-folder.id
}

# Read sensitive information from Bitwarden
data "bitwarden_item_login" "mysql-root-credentials" {
  id = "ec4e447f-9aed-4203-b834-c8f3848828f7"
}

# Later to be accessed as
#   ... = data.bitwarden_item_login.mysql-root-credentials.username
#   ... = data.bitwarden_item_login.mysql-root-credentials.password

data "bitwarden_item_secure_note" "ssh-private-key" {
  id = "a9e19f26-1b8c-4568-bc09-191e2cf56ed6"
}

# ....
```

<!-- schema generated by tfplugindocs -->
## Schema

### Optional

- **client_id** (String) Client API (required when authenticating with the official Bitwarden instances).
- **client_secret** (String) Client Secret (required when authenticating with the official Bitwarden instances). Do not commit this information in Git unless you know what you're doing. Prefer using a Terraform `variable {}` in order to inject this value from the environment.
- **email** (String) Login Email of the Vault.
- **master_password** (String) Master password of the Vault. Do not commit this information in Git unless you know what you're doing. Prefer using a Terraform `variable {}` in order to inject this value from the environment.
- **server** (String) Bitwarden server URL (default: https://vault.bitwarden.com).
- **vault_path** (String) Alternative directory for storing the Vault locally.

[Bitwarden]: https://bitwarden.com/help/article/managing-items/
